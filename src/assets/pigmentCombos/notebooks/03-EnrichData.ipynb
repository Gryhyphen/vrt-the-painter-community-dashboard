{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "// Import required packages\n",
    "#r \"nuget: FSharp.Data\"\n",
    "\n",
    "open System\n",
    "open System.IO\n",
    "open System.Text.RegularExpressions\n",
    "open FSharp.Data\n",
    "open System.Globalization\n",
    "open Microsoft.FSharp.Reflection\n",
    "\n",
    "type Lens<'T,'Field> =\n",
    "    { get : 'T -> 'Field\n",
    "      set : 'T -> 'Field -> 'T }\n",
    "\n",
    "module Lens =\n",
    "    /// Build a lens for a given property name (using reflection).\n",
    "    let forProperty<'T,'Field> (name: string) : Lens<'T,'Field> =\n",
    "        let tType = typeof<'T>\n",
    "        let prop =\n",
    "            tType.GetProperties()\n",
    "            |> Array.find (fun p -> p.Name = name && p.PropertyType = typeof<'Field>)\n",
    "        { get = fun t -> prop.GetValue(t) :?> 'Field\n",
    "          set = fun t v ->\n",
    "              let fields = FSharpType.GetRecordFields tType\n",
    "              let values =\n",
    "                  fields\n",
    "                  |> Array.map (fun f ->\n",
    "                      if f.Name = name then box v\n",
    "                      else f.GetValue(t))\n",
    "              FSharpValue.MakeRecord(tType, values) :?> 'T }\n",
    "\n",
    "// Define input/output paths\n",
    "let inputDir = \"../data/cleaned\"\n",
    "let outputPath = \"../data/enriched/enrichedPigments.json\"\n",
    "\n",
    "// Define a type alias for JSON provider\n",
    "type PigmentJson = JsonProvider<\"../data/cleaned/CleanedPigments-2025-10-05.json\">\n",
    "\n",
    "// Tasks\n",
    "let extractDate filename =\n",
    "    let m = Regex.Match(filename, @\"CleanedPigments-(\\d{4}-\\d{2}-\\d{2})\\.json\")\n",
    "    if m.Success then Some m.Groups.[1].Value else None\n",
    "\n",
    "let loadAndAddDate (file: string) =\n",
    "    match extractDate (Path.GetFileName(file)) with\n",
    "    | Some date ->\n",
    "        let data = PigmentJson.Load(file)\n",
    "        data\n",
    "        |> Array.map (fun r ->\n",
    "            {| \n",
    "                pigment1 = r.Pigment1\n",
    "                pigment2 = r.Pigment2\n",
    "                result = r.Result\n",
    "                datePainted = date\n",
    "            |})\n",
    "    | None -> [||]\n",
    "\n",
    "// [DedupePigments]--------------\n",
    "// How fun, very verbose but seemingly only way to work with structual typing\n",
    "// without having to define fully qualified types (i.e. adding steps before or after)\n",
    "// I would have to redo all the types.\n",
    "// And yeah, the inline is needed so that types can be resolved by the compiler.\n",
    "// (this is fine tho since the function is only called once)\n",
    "// VVVVVVVVVVVVVVVVVVV\n",
    "\n",
    "// Deduplicate pigments by unordered pair of pigments\n",
    "// Rules:\n",
    "// - Keep the earliest date overall\n",
    "// - If both with/without painter exist, drop the one without\n",
    "// - Allow multiple painters on the same day, but not the same painter twice\n",
    "let inline DedupePigments\n",
    "    (input: ^T array when ^T : (member pigment1 : string)\n",
    "                          and ^T : (member pigment2 : string)\n",
    "                          and ^T : (member datePainted : string)\n",
    "                          and ^T : (member painter : Option<string>)) =\n",
    "\n",
    "    // shorthand accessors\n",
    "    let inline p1 (x: ^T) = (^T : (member pigment1 : string) x)\n",
    "    let inline p2 (x: ^T) = (^T : (member pigment2 : string) x)\n",
    "    let inline date (x: ^T) = DateTime.Parse((^T : (member datePainted : string) x))\n",
    "    let inline painter (x: ^T) = (^T : (member painter : Option<string>) x)\n",
    "\n",
    "    input\n",
    "    |> Array.groupBy (fun r -> Set.ofList [ p1 r; p2 r ])\n",
    "    |> Array.collect (fun (_pigPair, records) -> // collect is just a flatMap\n",
    "        // find the earliest date among these records\n",
    "        let earliestDate =\n",
    "            records\n",
    "            |> Array.map date\n",
    "            |> Array.min\n",
    "\n",
    "        // keep only records on the earliest date\n",
    "        let sameDay =\n",
    "            records\n",
    "            |> Array.filter (fun r -> date r = earliestDate)\n",
    "\n",
    "        // if any paintered records exist, drop the painterless ones\n",
    "        let hasPainter =\n",
    "            sameDay |> Array.exists (fun r -> painter r |> Option.isSome)\n",
    "\n",
    "        let filtered =\n",
    "            if hasPainter then\n",
    "                sameDay |> Array.filter (fun r -> painter r |> Option.isSome)\n",
    "            else\n",
    "                sameDay\n",
    "\n",
    "        // dedupe by painter string (so only one per painter per day)\n",
    "        filtered\n",
    "        |> Array.distinctBy (fun r -> painter r |> Option.defaultValue \"\")\n",
    "    )\n",
    "\n",
    "let toTitleCase (s: string) =\n",
    "    CultureInfo.CurrentCulture.TextInfo.ToTitleCase(s.ToLower())\n",
    "\n",
    "let inline normalizeResults (input: 'T array) =\n",
    "    // TODO: Make this type safe - maybe try aether?\n",
    "    let resultLens = Lens.forProperty<'T,string> \"result\"\n",
    "    input\n",
    "    |> Array.map (fun r ->\n",
    "        let normalized = toTitleCase (resultLens.get r)\n",
    "        resultLens.set r normalized\n",
    "    )\n",
    "\n",
    "let inline EnrichFirstResultDiscovery (input: 'T array) =\n",
    "    // Extractors for reflection-based access\n",
    "    let resultLens = Lens.forProperty<'T,string> \"result\"\n",
    "    let dateLens   = Lens.forProperty<'T,string> \"datePainted\"\n",
    "\n",
    "    // Build a map of result -> earliest date\n",
    "    let earliestByResult =\n",
    "        input\n",
    "        |> Array.groupBy (fun r -> resultLens.get r)\n",
    "        |> Array.map (fun (res, records) ->\n",
    "            let minDate =\n",
    "                records\n",
    "                |> Array.map (fun r -> DateTime.Parse(dateLens.get r))\n",
    "                |> Array.min\n",
    "            res, minDate\n",
    "        )\n",
    "        |> Map.ofArray\n",
    "\n",
    "    // Enrich each record with the flag\n",
    "    input\n",
    "    |> Array.map (fun r ->\n",
    "        let res  = resultLens.get r\n",
    "        let date = DateTime.Parse(dateLens.get r)\n",
    "        let earliest = earliestByResult.[res]\n",
    "        {|\n",
    "            rest = r\n",
    "            firstResultDiscovery = (date = earliest)\n",
    "        |}\n",
    "    )\n",
    "\n",
    "type SubmissionsCsv = CsvProvider<\"../../pigmentSubmissions/pigmentSubmissions.csv\", HasHeaders=true>\n",
    "let paintersData = SubmissionsCsv.Load(\"../../pigmentSubmissions/pigmentSubmissions.csv\")\n",
    "\n",
    "let inline EnrichPainterData (input: 'T array) =\n",
    "    // Extractors for reflection-based access\n",
    "    let dateLens = Lens.forProperty<'T,string> \"datePainted\"\n",
    "    let pigment1Lens = Lens.forProperty<'T,string> \"pigment1\"\n",
    "    let pigment2Lens = Lens.forProperty<'T,string> \"pigment2\"\n",
    "\n",
    "    let combos =\n",
    "        input\n",
    "        |> Array.map (fun x -> (DateTime.Parse(dateLens.get x), pigment1Lens.get x, pigment2Lens.get x))\n",
    "    \n",
    "    // let newRows =\n",
    "    //     paintersData.Rows\n",
    "    //     |> Seq.toArray\n",
    "    //     |> Array.filter (fun x -> x.DatePainted.IsSome)\n",
    "        //|> Array.map (fun x -> (x, (x.DatePainted.Value, x.Pigment1, x.Pigment2)))\n",
    "        //|> Array.map (fun (x, key) -> (x, combos |> Array.contains key))\n",
    "        //|> Array.filter (fun (x,seen) -> not seen)\n",
    "\n",
    "    let newRowsFormatted =\n",
    "        paintersData.Rows\n",
    "        |> Seq.toArray\n",
    "        //|> Array.filter (fun x -> x.DatePainted.IsSome)\n",
    "        |> Array.map (fun (x) -> {|\n",
    "            pigment1 = x.Pigment1\n",
    "            pigment2 = x.Pigment2\n",
    "            result = x.Result\n",
    "            datePainted = x.DatePainted.ToString(\"yyyy-MM-dd\")\n",
    "            painter = Some x.Painter\n",
    "        |})\n",
    "    {|\n",
    "        rest = input\n",
    "        newRows = newRowsFormatted\n",
    "    |}\n",
    "\n",
    "\n",
    "// NOTE: Whenever you see an \"Enrich\" function, it immediately must be followed by a\n",
    "//       merge stategy that is defined in the pipeline (can't do merges inside the enrich function)\n",
    "//       there are probably ways around it (look at lens setters, can't add new fields but allows merges in-place)\n",
    "//       but for now, this enrich-merge convention is used.\n",
    "\n",
    "let output =\n",
    "    Directory.GetFiles(inputDir)\n",
    "    |> Array.collect loadAndAddDate\n",
    "    |> EnrichPainterData \n",
    "    |> fun x -> (x.rest |> Array.map (fun y -> {|y with painter = None|}) |> Array.append x.newRows) // Merge\n",
    "    |> DedupePigments\n",
    "    |> normalizeResults\n",
    "    |> EnrichFirstResultDiscovery\n",
    "    |> Array.map (fun x -> {|x.rest with firstResultDiscovery = x.firstResultDiscovery|}) // Merge\n",
    "\n",
    "output\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1b225b37",
   "metadata": {
    "language_info": {
     "name": "polyglot-notebook"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "outputs": [],
   "source": [
    "open System.Text.Json\n",
    "open System.IO\n",
    "\n",
    "// Serialize enriched pigment data to JSON\n",
    "let jsonOptions = JsonSerializerOptions(WriteIndented = true)\n",
    "let enrichedJson = JsonSerializer.Serialize(output, jsonOptions)\n",
    "\n",
    "// Ensure the output directory exists\n",
    "let enrichedDir = \"../data/enriched\"\n",
    "Directory.CreateDirectory(enrichedDir) |> ignore\n",
    "\n",
    "// Write to file\n",
    "let enrichedPath = Path.Combine(enrichedDir, \"enrichedPigments.json\")\n",
    "File.WriteAllText(enrichedPath, enrichedJson)\n",
    "\n",
    "printfn \"âœ… Enriched pigment data written to %s\" enrichedPath\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "name": "polyglot-notebook"
  },
  "polyglot_notebook": {
   "kernelInfo": {
    "defaultKernelName": "csharp",
    "items": [
     {
      "aliases": [],
      "name": "csharp"
     }
    ]
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
